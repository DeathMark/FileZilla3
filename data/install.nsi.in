;NSIS Modern User Interface version 1.70
;FileZilla 3 installation script
;Written by Tim Kosse <mailto:tim.kosse@gmx.de>

;--------------------------------
; Build environment

  !define top_srcdir @top_srcdir@
  !define srcdir @srcdir@

;--------------------------------
;Include Modern UI

  !include "MUI.nsh"

;--------------------------------
;General

  ;Name and file
  Name "FileZilla 3 alpha 2"
  OutFile "FileZilla_3_setup.exe"

  SetCompressor /SOLID LZMA

  ;Default installation folder
  InstallDir "$PROGRAMFILES\FileZilla 3"
  
  ;Get installation folder from registry if available
  InstallDirRegKey HKCU "Software\FileZilla 3" ""

;--------------------------------
;Variables

  Var MUI_TEMP
  Var STARTMENU_FOLDER

;--------------------------------
;Interface Settings

  !define MUI_ICON "${top_srcdir}/src/interface/resources/FileZilla.ico"
  !define MUI_UNICON "${srcdir}/uninstall.ico"

  !define MUI_ABORTWARNING

;--------------------------------
;Pages

  !insertmacro MUI_PAGE_LICENSE "${top_srcdir}\COPYING"
  !insertmacro MUI_PAGE_COMPONENTS
  !insertmacro MUI_PAGE_DIRECTORY

  ;Start Menu Folder Page Configuration
  !define MUI_STARTMENUPAGE_REGISTRY_ROOT "HKCU" 
  !define MUI_STARTMENUPAGE_REGISTRY_KEY "Software\FileZilla 3" 
  !define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "FileZilla 3"
  !define MUI_STARTMENUPAGE_DEFAULTFOLDER "FileZilla 3"
  
  !insertmacro MUI_PAGE_STARTMENU Application $STARTMENU_FOLDER

  !insertmacro MUI_PAGE_INSTFILES
  
  !insertmacro MUI_UNPAGE_CONFIRM
  !insertmacro MUI_UNPAGE_INSTFILES
  
;--------------------------------
;Languages
 
  !insertmacro MUI_LANGUAGE "English"

;--------------------------------
;Installer Sections

Section "FileZilla 3" SecMain

  SetOutPath "$INSTDIR"
  
  File "..\src\interface\FileZilla.exe"
  File "${srcdir}\mingwm10.dll"
  File "${top_srcdir}\GPL.html"

  SetOutPath "$INSTDIR\resources"
  File "${top_srcdir}/src\interface\resources\*.xrc"
  File "${top_srcdir}/src\interface\resources\*.png"
  File "${top_srcdir}/src\interface\resources\*.xpm"
  File "${top_srcdir}/src\interface\resources\themes.xml"

  SetOutPath "$INSTDIR\resources\16x16"
  File "${top_srcdir}/src\interface\resources\16x16\*.png"

  SetOutPath "$INSTDIR\resources\32x32"
  File "${top_srcdir}/src\interface\resources\32x32\*.png"
  
  SetOutPath "$INSTDIR\resources\cyril\16x16"
  File "${top_srcdir}/src\interface\resources\cyril\16x16\*.png"

  SetOutPath "$INSTDIR\resources\cyril\32x32"
;  File "${top_srcdir}/src\interface\resources\cyril\32x32\*.png"
  
  ;Store installation folder
  WriteRegStr HKCU "Software\FileZilla 3" "" $INSTDIR
  
  ;Create uninstaller
  WriteUninstaller "$INSTDIR\Uninstall.exe"

  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
    
  ;Create shortcuts
  CreateDirectory "$SMPROGRAMS\$STARTMENU_FOLDER"
  CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\Uninstall.lnk" "$INSTDIR\Uninstall.exe"
  CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\FileZilla 3.lnk" "$INSTDIR\filezilla.exe"
  
  !insertmacro MUI_STARTMENU_WRITE_END

SectionEnd

!macro INSTALLLANGFILE LANG

  SetOutPath "$INSTDIR\locales\${LANG}"
  File /oname=filezilla.mo "..\locales\${LANG}.mo"
  
!macroend

Section "Language files" SecLang

  ; installlangfiles.nsh is generated by configure and just contains a series of
  ; !insertmacro INSTALLLANGFILE <lang>
  !include installlangfiles.nsh

SectionEnd

;--------------------------------
;Descriptions

  ;Language strings
  LangString DESC_SecMain ${LANG_ENGLISH} "Required program files."
  LangString DESC_SecLang ${LANG_ENGLISH} "Language files files."

  ;Assign language strings to sections
  !insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
    !insertmacro MUI_DESCRIPTION_TEXT ${SecMain} $(DESC_SecMain)
    !insertmacro MUI_DESCRIPTION_TEXT ${SecLang} $(DESC_SecLang)
  !insertmacro MUI_FUNCTION_DESCRIPTION_END

;--------------------------------
;Uninstaller Section

!macro UNINSTALLLANGFILE LANG

  Delete "$INSTDIR\locales\${LANG}\filezilla.mo"
  RMDir "$INSTDIR\locales\${LANG}"

!macroend

Section "Uninstall"

  Delete "$INSTDIR\filezilla.exe"
  Delete "$INSTDIR\mingwm10.dll"
  Delete "mingwm10.dll"
  Delete "$INSTDIR\GPL.html"
  Delete "$INSTDIR\resources\*.xrc"
  Delete "$INSTDIR\resources\*.png"
  Delete "$INSTDIR\resources\*.xpm"
  Delete "$INSTDIR\resources\themes.xml"
  Delete "$INSTDIR\resources\16x16\*.png"
  Delete "$INSTDIR\resources\32x32\*.png"
  Delete "$INSTDIR\resources\cyril\16x16\*.png"
  Delete "$INSTDIR\resources\cyril\32x32\*.png"

  ; uninstalllangfiles.nsh is generated by configure and just contains a series of
  ; !insertmacro UNINSTALLLANGFILE <lang>
  !include uninstalllangfiles.nsh

  Delete "$INSTDIR\Uninstall.exe"

  RMDir "$INSTDIR\locales"
  RMDir "$INSTDIR\resources\32x32"
  RMDir "$INSTDIR\resources\16x16"
  RMDir "$INSTDIR\resources\cyril\32x32"
  RMDir "$INSTDIR\resources\cyril\16x16"
  RMDir "$INSTDIR\resources\cyril"
  RMDir "$INSTDIR\resources"
  RMDir "$INSTDIR"

  
  !insertmacro MUI_STARTMENU_GETFOLDER Application $MUI_TEMP
    
  Delete "$SMPROGRAMS\$MUI_TEMP\Uninstall.lnk"
  Delete "$SMPROGRAMS\$MUI_TEMP\FileZilla 3.lnk"
  
  ;Delete empty start menu parent diretories
  StrCpy $MUI_TEMP "$SMPROGRAMS\$MUI_TEMP"
 
  startMenuDeleteLoop:
    RMDir $MUI_TEMP
    GetFullPathName $MUI_TEMP "$MUI_TEMP\.."
    
    IfErrors startMenuDeleteLoopDone
  
    StrCmp $MUI_TEMP $SMPROGRAMS startMenuDeleteLoopDone startMenuDeleteLoop
  startMenuDeleteLoopDone:

  DeleteRegKey /ifempty HKCU "Software\FileZilla 3"

SectionEnd
